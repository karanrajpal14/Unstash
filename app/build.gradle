apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion
    defaultConfig {
        applicationId "rajpal.karan.unstash"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "REDDIT_CLIENT_ID", '\"NaP7nvfW4D721A\"')
        buildConfigField("String", "REDDIT_REDIRECT_URL", '\"http://www.reddit.com\"')
    }
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
            zipAlignEnabled true
            debuggable false
            multiDexEnabled true
        }
        debug {
            applicationIdSuffix ".debug"
            multiDexEnabled true
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile project(':library')

    compile "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:design:$rootProject.supportLibraryVersion"
    compile "com.android.support.constraint:constraint-layout:$rootProject.constraintLayoutVersion"
    compile "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"
    compile "net.dean.jraw:JRAW:$rootProject.JRAWVersion"
    compile "com.jakewharton:butterknife:$rootProject.butterKnifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.butterKnifeVersion"
    compile "com.jakewharton.timber:timber:$rootProject.timberVersion"
    compile "org.slf4j:slf4j-android:$rootProject.sl4jVersion"
    debugCompile "com.facebook.stetho:stetho:$rootProject.stethoVersion"
    compile "com.github.bumptech.glide:glide:$rootProject.glideVersion"
    compile "com.android.support:support-v4:$rootProject.supportLibraryVersion"
    annotationProcessor "com.github.bumptech.glide:compiler:$rootProject.glideVersion"
    compile "com.github.zagum:Android-SwitchIcon:$rootProject.switchIconVersion"
    compile "com.firebase:firebase-jobdispatcher:$rootProject.firebaseJobDispatcherVersion"
    compile "com.github.bachors:Android-WordToSpan:$rootProject.wordToSpanVersion"
    compile "com.google.firebase:firebase-ads:$rootProject.googlePlayServicesVersion"
    compile "com.google.firebase:firebase-core:$rootProject.googlePlayServicesVersion"
    compile "com.android.support:preference-v14:$rootProject.supportLibraryVersion"
    compile "com.wdullaer:materialdatetimepicker:$rootProject.materialDatePickerVersion"
    compile 'io.reactivex.rxjava2:rxjava:2.0.3'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    testCompile 'junit:junit:4.12'
}

apply plugin: 'com.google.gms.google-services'